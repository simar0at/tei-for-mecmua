<xsl:stylesheet 
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
  xmlns:tei="http://www.tei-c.org/ns/1.0"
  xmlns="http://www.tei-c.org/ns/1.0"
  xpath-default-namespace="http://www.tei-c.org/ns/1.0"
  version="2.0"
>
<!-- This library is free software; you can redistribute it and/or
      modify it under the terms of the GNU Lesser General Public License as
      published by the Free Software Foundation; either version 2.1 of the
      License, or (at your option) any later version. This library is
      distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
      without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
      PARTICULAR PURPOSE. See the GNU Lesser General Public License for more
      details. You should have received a copy of the GNU Lesser General Public
      License along with this library; if not, write to the Free Software
      Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA
      02111-1307 USA 

$Id: iden.xsl 4549 2008-04-23 16:40:01Z rahtz $

2008, TEI Consortium
-->

<xsl:import href="getfiles.xsl"/>
<!-- 
read a corpus of TEI P5 documents and construct
an ODD customization file which expresses the subset
of the TEI you need to validate that corpus
-->
<xsl:output indent="yes"/>
<!-- name of odd -->
<xsl:param name="schema">oddbyexample</xsl:param>
<!-- the document corpus -->
<xsl:param name="corpus">./</xsl:param>
<!-- the source of the TEI (just needs *Spec)-->
<xsl:param name="tei">/usr/share/xml/tei/odd/p5subset.xml</xsl:param>

<xsl:key name="ELEMENTS" use="1" match="tei:elementSpec"/>
<xsl:key name="Used" use="@ident" match="docs/element"/>
<!--
1) start a variable and copy in all of the TEI 
2) read the corpus and get a list of all the elements and their
attributes that it uses
3) process the variable and read the TEI section. if an element or
 attribute is not present in the corpus section, put out a delete
 customization
-->

<xsl:template name="processAll">

 <xsl:variable name="stuff">
   <all>
     <tei>
       <xsl:for-each select="document($tei)">
	 <xsl:for-each select="key('ELEMENTS',1)">
	   <element>
	     <xsl:copy-of select="@ident"/>
	     <xsl:copy-of select="@module"/>
	     <xsl:for-each select=".//tei:attDef">
	       <attribute>
		 <xsl:copy-of select="@ident"/>
	       </attribute>
	     </xsl:for-each>
	   </element>
	 </xsl:for-each>
       </xsl:for-each>
     </tei>
     <docs>
       <xsl:for-each-group select="key('All',1)" group-by="local-name()">
	 <xsl:sort select="current-grouping-key()"/>
	 <element>
	   <xsl:attribute name="ident">
	     <xsl:copy-of select="current-grouping-key()"/>
	 </xsl:attribute>
	 </element>
       </xsl:for-each-group>
     </docs>
   </all>
 </xsl:variable>

 <xsl:variable name="results">
   <schemaSpec>
     <xsl:for-each select="$stuff/all/tei/element">
       <xsl:choose>
	 <xsl:when test="key('Used',@ident)">
	   <elementSpec ident="{@ident}" module="{@module}" mode="keep"/>
	 </xsl:when>
	 <xsl:otherwise>
	   <elementSpec ident="{@ident}" module="{@module}" mode="delete"/>
	 </xsl:otherwise>
       </xsl:choose>
     </xsl:for-each>
   </schemaSpec>
 </xsl:variable>
 <TEI xml:lang="en">
   <teiHeader>
      <fileDesc>
         <titleStmt>
            <title>TEI customization based on <xsl:value-of select="$corpus"/></title>
         </titleStmt>
         <publicationStmt>
	   <p> </p>
	 </publicationStmt>
         <sourceDesc>
            <p>generated by oddbyexample.xsl</p>
         </sourceDesc>
      </fileDesc>
   </teiHeader>
<text>
<body>
 <schemaSpec ident="{$schema}">
   <moduleRef key="tei"/>
   <moduleRef key="core"/>
   <xsl:for-each-group select="$results/schemaSpec/elementSpec[@mode='keep']" group-by="@module">
     <xsl:sort select="current-grouping-key()"/>
     <xsl:choose>
       <xsl:when test="@module='core'"/>
       <xsl:otherwise>
	 <moduleRef key="{@module}"/>
       </xsl:otherwise>
     </xsl:choose>
     <xsl:copy-of
	 select="$results/schemaSpec/elementSpec[@mode='delete' and @module=current-grouping-key()]"/>
   </xsl:for-each-group>
 </schemaSpec>

</body>
</text>
</TEI>

</xsl:template>

</xsl:stylesheet>
